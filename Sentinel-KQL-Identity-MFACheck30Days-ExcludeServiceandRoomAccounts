// Users with No MFA Sign-ins in the last 30 days - Only Single Factor

SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType == "0" // Successful sign-ins only
| where not(UserPrincipalName startswith "srv") and not(UserPrincipalName startswith "room") // Exclude accounts starting with srv & room
| summarize count() by UserPrincipalName, AuthenticationRequirement
| where AuthenticationRequirement == "singleFactorAuthentication"
| join kind=anti (
    SigninLogs
    | where TimeGenerated > ago(30d)
    | where AuthenticationRequirement == "multiFactorAuthentication"
    | where not(UserPrincipalName startswith "srv") and not(UserPrincipalName startswith "room") // Exclude accounts starting with srv & room
    | summarize by UserPrincipalName
) on UserPrincipalName
| sort by count_ desc
